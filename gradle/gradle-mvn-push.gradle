/*
 * Copyright 2013 Chris Banes
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'maven'
apply plugin: 'signing'

def isReleaseBuild() {
    return android.defaultConfig.versionName.contains("SNAPSHOT") == false
}

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                pom.groupId = "io.relayr"
                pom.artifactId = "android-sdk"
                pom.version = android.defaultConfig.versionName

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    //read from ~/.gradle/gradle.properties
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }
                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    //read from ~/.gradle/gradle.properties
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                    name "Relayr Android SDK"
                    packaging "jar"
                    description "SDK for connecting to the Relayr Cloud"
                    url "https://github.com/relayr/android-sdk/"

                    scm {
                        url 'scm:git@github.com:relayr/android-sdk.git'
                        connection 'scm:git@github.com:relayr/android-sdk.git'
                        developerConnection 'scm:git@github.com:relayr/android-sdk.git'
                    }

                    licenses {
                        license {
                            name 'The MIT License'
                            url 'http://ithings4u.mit-license.org/'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'hugodomenechjuarez'
                            name 'Hugo Doménech Juárez'
                        }
                        developer {
                            id 'yerraycallero'
                            name 'Yeray Callero'
                        }
                    }
                }
            }
        }
    }

    signing {
        required { isReleaseBuild() && gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }

    task androidJavadocsJar(type: Jar, dependsOn: bundleJavadocRelease) {
        classifier = 'javadoc'
        from bundleJavadocRelease.destinationDir
    }

    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.sourceFiles
    }

    artifacts {
        archives androidSourcesJar
        archives androidJavadocsJar
    }
}
